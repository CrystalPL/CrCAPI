plugins {
    id 'java-library'
    id "com.github.johnrengelman.shadow" version "7.1.0"
}

allprojects {
    group = 'pl.crystalek'
    version = '1.0-SNAPSHOT'

    apply plugin: 'java-library'
    apply plugin: 'com.github.johnrengelman.shadow'

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    compileJava.options.encoding = 'UTF-8'
}

subprojects {
    repositories {
        mavenCentral()

        maven {
            name = 'spigotmc-repo'
            url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
        }

        maven {
            name = 'sonatype'
            url = 'https://oss.sonatype.org/content/groups/public/'
        }
    }

    dependencies {
        shadow ('org.spigotmc:spigot-api:1.8.8-R0.1-SNAPSHOT')
        shadow ('org.projectlombok:lombok:1.18.22')
        annotationProcessor ('org.projectlombok:lombok:1.18.22')
    }

    shadowJar {
        relocate 'net.kyori', 'pl.crystalek.crcapi.lib.adventure'
        relocate 'net.kyori.examination', 'pl.crystalek.crcapi.lib.examination'

        relocate 'com.mongodb', 'pl.crystalek.crcapi.lib.mongodb'
        relocate 'com.zaxxer', 'pl.crystalek.crcapi.lib.hikari'
        relocate 'com.mysql', 'pl.crystalek.crcapi.lib.mysql'
        relocate 'org.bson', 'pl.crystalek.crcapi.lib.bson'
        relocate 'org.slf4j', 'pl.crystalek.crcapi.lib.slf4j'
        relocate 'com.google.protobuf', 'pl.crystalek.crcapi.lib.protobuf'
        relocate 'google.protobuf', 'pl.crystalek.crcapi.lib.protobuf'

        classifier(null)
        destinationDirectory.set(new File(rootProject.rootDir, "api"))
    }
}

shadowJar.enabled = false